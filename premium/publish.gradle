apply plugin: 'maven-publish'

/*
Publish the library to the remote GitHub maven repo:
./gradlew clean assembleRelease publish

Publish the library to the local mavend repo:
./gradlew clean assembleRelease publishToMavenLocal

To use locally published library go to to root build.gradle > allProjects > repositories
and replace GitHub maven repo declaration with mavenLocal()
 */

static def setupDependencies(dependenciesNode, dependencies) {
    dependencies.each {
        def dependencyNode = dependenciesNode.appendNode('dependency')
        dependencyNode.appendNode('groupId', it.group)
        dependencyNode.appendNode('artifactId', it.name)
        dependencyNode.appendNode('version', it.version)
    }
}

def GROUP_ID = 'com.pivotmaster.premium'
def ARTIFACT_ID = 'premium'
def VERSION_NAME = getLibraryVersion()

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/PivotMasterApps/premium")
            credentials {
                username = project.findProperty("githubUsername") ?: System.getenv("USERNAME_GITHUB")
                password = project.findProperty("githubToken") ?: System.getenv("TOKEN_GITHUB")
            }
        }
    }
    publications {
        regular(MavenPublication) {
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION_NAME
            artifact("$buildDir/outputs/aar/${getPremiumArchiveBaseName()}-release.aar")
            artifact(sourceJar)
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                setupDependencies(dependenciesNode, configurations.api.allDependencies)
            }
        }
    }
}