// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
}

def LIBRARY_VERSION = "3.2.0"

static def getGitBranchName() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

task getLibraryVersionTask {
    doLast {
        println getLibraryVersion()
    }
}

ext.getPremiumArchiveBaseName = {
    return "premium-${getLibraryVersion()}"
}

ext.getLibraryVersion = {
    def branchName = getGitBranchName()
    def version = ""

    if (branchName == "develop") {
        version = LIBRARY_VERSION + "-SNAPSHOT"
    } else if (branchName == "master") {
        version = LIBRARY_VERSION
    } else {
        version = "$LIBRARY_VERSION-${branchName.replaceAll("/", "-")}-SNAPSHOT"
    }

    return version
}

task clean(type: Delete) {
    delete rootProject.buildDir
}